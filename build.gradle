plugins {
	id "org.jetbrains.kotlin.jvm" version "1.4.10"
	id "org.jetbrains.dokka"      version "1.4.0-rc"
	id "maven-publish"
}

group = "strixpyrr.specular"
archivesBaseName = "Specular"
version = "0.7"

allprojects {
	repositories {
		mavenCentral()
		jcenter()
		mavenLocal()
	}
}

subprojects {
	apply plugin: "org.jetbrains.kotlin.jvm"

	dependencies {
		implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
	}
}

dependencies {
	implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
	implementation "org.jetbrains.kotlin:kotlin-reflect"
	// implementation "com.google.guava:guava:21.+"
	implementation "org.apache.commons:commons-collections4:4.4"
	implementation "uy.kohesive.klutter:klutter-core:3.0.+"

	implementation "strixpyrr.abstrakt:Abstrakt:0.1"
	implementation "strixpyrr.abstrakt:Abstrakt.Collections:0.0.2"
}

dokkaHtml {
	dokkaSourceSets {
		configureEach {
			includes = ["src/main/kotlin/Packages.md"]

			perPackageOption {
				prefix = "strixpyrr.specular.models.internal"
				suppress = true
			}

			perPackageOption {
				prefix = "strixpyrr.specular.models.factory.internal"
				suppress = true
			}
		}
	}
}

task dokkaJar(type: Jar) {
	from dokkaHtml
	archiveClassifier.set("docs")
}

compileKotlin {
	kotlinOptions {
		jvmTarget = "1.8"
		languageVersion = "1.4"
		freeCompilerArgs +=
		[
			"-Xinline-classes",
			"-Xopt-in=kotlin.contracts.ExperimentalContracts",
			"-Xopt-in=kotlin.ExperimentalStdlibApi"
		]
	}
}

publishing {
	publications {
		specular(MavenPublication) {
			from components.kotlin

			artifact kotlinSourcesJar
			artifact dokkaJar
		}
	}
}